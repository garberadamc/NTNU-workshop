title:
    Continuous random intercept LTA    
data:
    file = chap11.txt;
variable:
    names = a b c d e male female count;
    usev = a-e;
    freqweight = count;
    missing = all(-9999);
    categorical = a-e;
    classes = ca(2) cb(2) cc(2) cd(2) ce(2);

analysis:
    type = mixture; 
    algorithm = integration;
    starts = 80 16;
    processors = 8;


model:
    %overall%
   
    cb on ca;
    cc on cb;
    cd on cc;
    ce on cd;

    f by a-e* (lam);
    f@1; [f@0];

model ca:
    %ca#1%
    [a$1] (3);
    %ca#2%
    [a$1] (4);
model cb:
    %cb#1%
    [b$1] (3);
    %cb#2%
    [b$1] (4);
model cc:
    %cc#1%
    [c$1] (3);
    %cc#2%
    [c$1] (4);
model cd:
    %cd#1%
    [d$1] (3);
    %cd#2%
    [d$1] (4);
model ce:
    %ce#1%
    [e$1] (3);
    %ce#2%
    [e$1] (4);

Output:  tech15;
    

